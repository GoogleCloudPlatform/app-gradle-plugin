/*
 * Copyright (c) 2017 Google Inc. All Right Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'maven-publish'

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

// this is the only way for us to create a pom using the gradle tools
// usually this same config is used to deploy, but we're deploying
// using another tool and really just want the pom.
publishing {
  publications {
    release(MavenPublication) {
      pom.withXml {
        def root = asNode()
        root.appendNode('name', 'App Engine Gradle Plugin')
        root.appendNode('description', 'This Gradle plugin provides tasks to build and deploy Google App Engine applications.')
        root.appendNode('url', 'https://github.com/GoogleCloudPlatform/app-gradle-plugin')
        root.appendNode('inceptionYear', '2016')

        def scm = root.appendNode('scm')
        scm.appendNode('url', 'https://github.com/GoogleCloudPlatform/app-gradle-plugin')
        scm.appendNode('connection', 'scm:https://github.com/GoogleCloudPlatform/app-gradle-plugin.git')
        scm.appendNode('developerConnection', 'scm:git://github.com/GoogleCloudPlatform/app-gradle-plugin.git')
        
        def license = root.appendNode('licenses').appendNode('license')
        license.appendNode('name', 'The Apache Software License, Version 2.0')
        license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
        license.appendNode('distribution', 'repo')

        def developer = root.appendNode('developers').appendNode('developer')
        developer.appendNode('id', 'loosebazooka')
        developer.appendNode('name', 'Appu Goundan')
        developer.appendNode('email', 'appu@google.com')
      }
    }
  }
}

task prepareRelease(type: Copy) {
  from jar
  from sourceJar
  from javadocJar
  project.afterEvaluate {
    // workaround for lazily generated pom publication task 
    // https://discuss.gradle.org/t/gradlew-tasks-fails-in-2-4-due-to-not-finding-generatepom-task/10097/9
    tasks.realize() 
    from tasks.generatePomFileForReleasePublication
    rename("pom-default.xml", "pom-${project.version}.xml")
  }

  into "${buildDir}/release"

  dependsOn build
  dependsOn cleanPrepareRelease
}
